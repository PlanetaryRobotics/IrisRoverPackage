# Automated behavior that attempts to turn on the wifi radio.
# If turning on the radio fails, it catches it and retries after a delay.
# If it fails too many times, it restarts the radio.
# ! Update state_driver.puml if you make changes here.

# Handle for the single shot timer to know what event triggered it
const TIMER_HANDLE = $be00
# Max number of times to try before resetting:
const COUNTER_MAX = 5
# Time to wait before trying again, in ms:
const RETRY_WAIT_MS = 200

# How many times it's been tried unsuccessfully so far.
dim counter

event system_boot(major,minor,patch,build,bootloader,tcpip,hw)
    # init vars:
    counter = 0
end

export procedure WIFI_ON()
    counter = 0
    call sme_set_operating_mode(1)  # Make sure radio is in station mode (not AP)
    call sme_wifi_on()
end


event sme_wifi_is_on(result)
	# If Wi-Fi is ON
	if result = 0 then
        counter = 0
	else
        counter = counter + 1
		# Radio failed to turn on for some reason...
		if (counter < COUNTER_MAX) then
            call hardware_set_soft_timer(RETRY_WAIT_MS, TIMER_HANDLE, 1)
        else
            # failed too many times. something must be wrong. Reboot!
		    call system_reset(0)
        end if
	end if
end


event hardware_soft_timer(handle)
    if handle = TIMER_HANDLE then
        call sme_wifi_on()
    end if
end
